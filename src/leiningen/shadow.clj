(ns leiningen.shadow
  (:require [leiningen.run]))

(def ^:const run-shadow-cljs ["-m" "shadow.cljs.devtools.cli"])
(def ^:const shadow-cljs-preamble ";; This file is generated by lein-shadow, do not manually edit. Instead, edit project.clj shadow-cljs key.\n")

(defn shadow
  "Helps keep your project configuration in your project.clj file when using shadow-cljs.

Refer to shadow-cljs docs for exhaustive CLI args, some possible args include:

  compile [build]
  release [build]
  watch   [build]"
  [project & args]
  (if-let [config (:shadow-cljs project)]
    (let [args' (concat run-shadow-cljs args)]
      (spit "shadow-cljs.edn" (str shadow-cljs-preamble (pr-str config)))
      (apply leiningen.run/run project args'))
    (leiningen.core.main/warn "No shadow-cljs config key defined in project.clj. Please add a config to go into shadow-cljs.edn")))
